(
(data Bool (True False))
(:: True Bool)
(:: False Bool)

(data Nat (Z S))
(:: Z Nat)
(:: S (-> (Nat) Nat))

(data List (Nil Cons))
(:: Nil  (List ?a)                    )
(:: Cons (-> (?a (List ?a)) (List ?a)))

(:: ite (-> (Bool ?a ?a) ?a))
(=> (ite True ?x ?y ) ?x)
(=> (ite False ?x ?y) ?y)

(:: add (-> (Nat Nat) Nat))
(=> (add Z      ?y) ?y             )
(=> (add (S ?x) ?y) (S (add ?x ?y)))

(:: sub (-> (Nat Nat) Nat))
(=> (sub ?x Z)          ?x         )
(=> (sub Z ?y)          Z          )
(=> (sub (S ?x) (S ?y)) (sub ?x ?y))

(:: leq (-> (Nat Nat) Bool))
(=> (leq Z      ?y) True           )
(=> (leq (S ?x) Z ) False          )
(=> (leq (S ?x) (S ?y)) (leq ?x ?y))

(:: lt (-> (Nat Nat) Bool))
(=> (lt ?x     Z     ) False     )
(=> (lt Z      (S ?y)) True      )
(=> (lt (S ?x) (S ?y)) (lt ?x ?y))

(:: eq (-> (Nat Nat) Bool))
(=> (eq Z      Z     ) True      )
(=> (eq Z      (S ?y)) False     )
(=> (eq (S ?x) Z     ) False     )
(=> (eq (S ?x) (S ?y)) (eq ?x ?y))

(:: len (-> ((List ?a)) Nat))
(=> (len Nil          ) Z            )
(=> (len (Cons ?x ?xs)) (S (len ?xs)))

(:: elem (-> (Nat (List Nat)) Bool))
(=> (elem ?n Nil          ) False                            )
(=> (elem ?n (Cons ?x ?xs)) (ite (eq n x) True (elem ?n ?xs)))

(:: take (-> (Nat (List ?a)) (List ?a)))
(=> (take Z      ?xs          ) Nil                    )
(=> (take (S ?n) Nil          ) Nil                    )
(=> (take (S ?n) (Cons ?x ?xs)) (Cons ?x (take ?n ?xs)))

(:: drop (-> (Nat (List ?a)) (List ?a)))
(=> (drop Z      ?xs          ) ?xs          )
(=> (drop (S ?n) Nil          ) Nil          )
(=> (drop (S ?n) (Cons ?x ?xs)) (drop ?n ?xs))

(:: count (-> (Nat (List Nat)) Nat))
(=> (count ?x Nil          ) Z)
(=> (count ?x (Cons ?y ?ys)) (ite (eq ?x ?y) (S (count ?x ?ys)) (count ?x ?ys)))

(:: append (-> ((List ?a) (List ?a)) (List ?a)))
(=> (append Nil           ?ys) ?ys                       )
(=> (append (Cons ?x ?xs) ?ys) (Cons ?x (append ?xs ?ys)))

(:: map (-> ((-> ?a ?b) (List ?a)) (List ?b)))
(=> (map ?f Nil          ) Nil                          )
(=> (map ?f (Cons ?x ?xs)) (Cons (@ ?f ?x) (map ?f ?xs)))

(:: filter (-> (-> ?a Bool) (List ?a)) (List ?a))
(=> (filter ?p Nil          ) Nil                                                     )
(=> (filter ?p (Cons ?x ?xs)) (ite (@ ?p ?x) (Cons ?x (filter ?p ?xs)) (filter ?p ?xs)))

(:: ins (-> (Nat (List Nat)) (List Nat)))
(=> (ins ?n Nil          ) (Cons ?n Nil)                                                  )
(=> (ins ?n (Cons ?x ?xs)) (ite (lt ?n ?x) (Cons ?n (Cons ?x ?xs)) (Cons ?x (ins ?n ?xs))))

(:: insort (-> (Nat (List Nat)) (List Nat)))
(=> (insort ?n Nil          ) (Cons ?n Nil)                                                      )
(=> (insort ?n (Cons ?x ?xs)) (ite (leq ?n ?x) (Cons ?n (Cons ?x ?xs)) (Cons ?x (insort ?n ?xs))))

(:: ins1 (-> (Nat (List Nat)) (List Nat)))
(=> (ins1 ?n Nil          ) (Cons ?n Nil)                                                   )
(=> (ins1 ?n (Cons ?x ?xs)) (ite (eq ?n ?x) (Cons ?n (Cons ?x ?xs)) (Cons ?x (ins1 ?n ?xs))))

(:: sort (-> ((List Nat)) (List Nat)))
(=> (sort Nil          ) Nil                   )
(=> (sort (Cons ?x ?xs)) (insort ?x (sort ?xs)))

(=== prop01 (n xs) (Nat (List a))
  (append (take n xs) (drop n xs))
  xs
)

(=== prop02 (n xs ys) (Nat (List Nat) (List Nat))
  (add (count n xs) (count n ys))
  (count n (append xs ys))
)

(=== prop03 (n xs ys) (Nat (List Nat) (List Nat))
  (leq (count n xs) (count n (append xs ys)))
  True
)

(=== prop04 (n xs) (Nat (List Nat))
  (S (count n xs))
  (count n (Cons n xs))
)

(=== prop11 (xs) ((List a))
  (drop Z xs)
  xs
)

(=== prop12 (n f xs) (Nat (-> a b) (List a))
  (drop n (map f xs))
  (map f (drop n xs))
)

(=== prop13 (n x xs) (Nat a (List a))
  (drop (S n) (Cons x xs))
  (drop n xs)
)

(=== prop14 (p xs ys) ((-> a Bool) (List a) (List a))
  (filter p (append xs ys))
  (append (filter p xs) (filter p ys))
)

(=== prop15 (x xs) (Nat (List Nat))
  (len (ins x xs))
  (S (len xs))
)

(=== prop19 (n xs) (Nat (List a))
  (len (drop n xs))
  (sub (len xs) n)
)

(=== prop20 (xs) ((List Nat))
  (len (sort xs))
  (len xs)
)

(=== prop28 (x xs) (Nat (List Nat))
  (elem x (append xs (Cons x Nil)))
  True
)

(=== prop29 (x xs) (Nat (List Nat))
  (elem x (ins1 x xs))
  True
)

(=== prop30 (x xs) (Nat (List Nat))
  (elem x (ins x xs))
  True
)

(=== prop56 (n m xs) (Nat Nat (List a))
  (drop n (drop m xs))
  (drop (add n m) xs)
)
)